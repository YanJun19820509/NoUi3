// Effect Syntax Guide: https://github.com/cocos-creator/docs-3d/blob/master/zh/material-system/effect-syntax.md

CCEffect %{
  techniques:
  - passes:
    - vert: vs:vert 
      frag: unlit-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        direction: { value: 0., editor: { range: [-1.0, 1.0], tooltip: '左右方向' } }
        speed: { value: 1., editor: { tooltip: '速度' } }
        maxRadius: { value: 1., editor: { tooltip: '最大半径' } }
}%

CCProgram vs %{
  #include <common-vs>
}%

CCProgram unlit-fs %{
  precision highp float;
  #include <cc-global>
  #include <embedded-alpha>
  #include <alpha-test>
  #include <common-fs>
  #include <utils>
     
    uniform RainData{
      float direction;
      float speed;
      float maxRadius;
    };

  float  drawLayer(float scale,vec2 uv)
  {
    // 注释设 scale为10的情况
    // 输出值
    float co = 0.;

    // 拷贝一份uv用，不修改原uv
    vec2 uv1 = uv;

    // uv值范围从 0-1变为 0-scale scale值为10就是0-10范围
    uv1 *= scale;

    vec2 hvtemp = floor(uv1);
    uv1.x +=rand(vec2(hvtemp.y));

    // floor向下取整，计算出格子所在下标，
    // 10*10的格子，如果uv是 0.2345,0.2345,uv1就是2.345,2.345
    // 取整后 即为该uv所在格子下表，2,2
    // 0.2 <= uv < 0.3 该范围内所有uv坐标，处理后hv均为 2,2
    vec2 hv = floor(uv1);

    // fr是 fract对数字取小数部分， 0.2345,0.2345 -> uv1 2.345,2.345 -> 0.345,0.345    
    // 如 0.2 <= uv < 0.3 处理后就是一个范围 0-1的范围
    vec2 fr = fract(uv1);

    float strength = rand(hv); 

    // 画圆，用fr（即格内坐标）和 0.5,0.5的点的距离作为颜色值,circle范围0-0.5
    float ss = strength * maxRadius;//可以调整圆大小
    float circle = distance(fr,vec2(.5)) / ss;
    
    // 上面的结果是距离值计算出来的，有明暗变化，用step把圆内都变成纯白色
    // 用hv引入随机的半径 因为一格内hv相同，随机值结果也就是半径值相等
    // radius 0-1 映射到 0.1-0.2的范围
    float radius = strength * .1 + .1;
    // strength *= sin(cc_time.x) + 1.; 

    // 半径*10取整，对2取余，舍弃一半的圆
    float f1 = mod(floor(radius * 10.),2.);
    radius *= f1;

    //circle = step(radius,circle); 这一行注释掉，用下一行。这时模糊圆圈边缘的函数，0.02*scale就是模糊的宽度，这个系数也可以自己调整到喜欢的数值，
    // 这个系数和strength乘的系数调整个不同的值，组合起来效果也大不一样。
    circle = smoothstep(radius - .02 * scale,radius,circle);
    
    // 1. - circle 翻转色值，使距离圆心越近颜色越亮
    circle = 1. - circle;
    // 让层亮度和格子大小成正比比例 （scale是uv的缩放数，越大 格子就越小）
    // * 9 是因为有些暗，变亮点，这个值可以随便调调
    strength *= 1. / scale * 9.;
    circle *= strength;
    
    co += circle;
    // co += drawGird(fr);

    // 用fr.x+fr.y作为输出看一下结果
    // co += fr.x + fr.y;
    return co;
  }

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if IS_RAIN
      vec3 co = vec3(0.);
      for(int i = 0;i < 3;i++ ){
        float idx = float(i);
        // 用循环下表做一个递增的层半径
        float p1 = idx * 5. + 3.;

        // 给每一层做一个随机运动方向 也就是一个速度向量
        vec2 uvoff = vec2(rand(vec2(p1 * 10.0)),rand(vec2(p1 * 10.0)));
        // 速度*时间 = 偏移距离 让该层随时间运动 可以注释掉 *u_time 就不会运动了
        uvoff = uvoff *cc_time.x * speed +.9;
        
        vec2 p2 = vec2(uv0.x - uvoff.x * direction,uv0.y - uvoff.y);

        // p1 半径， p2 供计算的uv值
        float layer = drawLayer(p1,p2);
        
        co += vec3(layer);
      }
      o.rgb *= co.rgb;
    #elif USE_TEXTURE
      o *= normalTexture(cc_spriteTexture, uv0);
    #endif

    o += normalTexture(cc_spriteTexture, uv0);
    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
